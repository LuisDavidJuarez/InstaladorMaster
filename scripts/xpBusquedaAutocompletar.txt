CREATE PROC [dbo].[xpBusquedaAutocompletar]
	@opcion INT,
	@texto varchar(50)
AS BEGIN
	DECLARE @Arts TABLE (Articulo VARCHAR(20), Descripcion VARCHAR(100), SustanciaActiva VARCHAR(100), MasParecido INT)
	DECLARE @SustanciaActiva TABLE (SustanciaActiva VARCHAR(100))
	DECLARE @articulos TABLE (Articulo VARCHAR(20))
    
	IF rtrim(ISNULL(@texto,'')) <> ''
	BEGIN
		IF @opcion = 1
		BEGIN
			INSERT @Arts
			SELECT Articulo, Descripcion1, SustanciaActiva, 1
			  FROM Art WITH (NOLOCK)
			 WHERE Descripcion1 LIKE '%' + @texto + '%'
			   AND (Familia LIKE '%GENERICO%' OR Familia LIKE '%PATENTE%')
			   AND Grupo NOT IN('CADUCADO')

			INSERT @Arts
			SELECT Articulo, Descripcion1, SustanciaActiva, 2
			  FROM Art WITH (NOLOCK)
			 WHERE DIFFERENCE(Descripcion1,@texto) = 4
			   AND SOUNDEX(Descripcion1) = SOUNDEX(@texto)
			   AND Articulo NOT IN (SELECT Articulo FROM @Arts)
			   AND (Familia LIKE '%GENERICO%' OR Familia LIKE '%PATENTE%')
			   AND Grupo NOT IN('CADUCADO')
		END
		ELSE IF @opcion = 2
		BEGIN
			INSERT INTO @SustanciaActiva (SustanciaActiva)
			SELECT SUBSTRING(A.SustanciaActiva 
						,LEN(A.SustanciaActiva) - (LEN(LTRIM(REPLACE(A.SustanciaActiva, '"', ' ')) + '|') - 1) + 1 --start_pos
						,LEN(LTRIM(REPLACE(A.SustanciaActiva, '"', ' '))) --length
				   ) AS SustanciaActiva
			  FROM Art AS A WITH(NOLOCK)
			 WHERE A.SustanciaActiva != ''
			   AND SustanciaActiva LIKE '%' + @texto + '%'
			   AND Grupo NOT IN('CADUCADO')
			 GROUP BY SustanciaActiva
		END
		ELSE
		BEGIN
			INSERT INTO @articulos
			SELECT Cuenta
			  FROM CB WITH (NOLOCK)
			 WHERE CB.Codigo LIKE @texto + '%'
			   AND CB.TipoCuenta = 'Articulos'
			
			IF EXISTS(SELECT * FROM @articulos)
			BEGIN
				INSERT INTO @Arts
				SELECT Articulo, Descripcion1, SustanciaActiva, 1
				  FROM Art WITH (NOLOCK)
				 WHERE Articulo IN (SELECT Articulo FROM @articulos)
				   AND Grupo NOT IN('CADUCADO')
			END
			ELSE
			BEGIN
				INSERT INTO @Arts
				SELECT Articulo, Descripcion1, SustanciaActiva, 1
				  FROM Art WITH (NOLOCK)
				 WHERE Articulo LIKE @texto + '%'
				   AND Grupo NOT IN('CADUCADO')
			END
		END

		IF EXISTS(SELECT * FROM @Arts)
		BEGIN
			SELECT Articulo, Descripcion, SustanciaActiva
			  FROM @Arts
			 ORDER BY MasParecido
		END
		ELSE IF EXISTS(SELECT * FROM @SustanciaActiva)
		BEGIN
			SELECT SustanciaActiva
			  FROM @SustanciaActiva
		END
	END
END